import Semantic.CC.Denotation.Core
import Semantic.CC.Denotation.Rebind
namespace CC

/-- Interpret a variable in an environment to get its free variable index. -/
def interp_var (env : TypeEnv s) (x : Var .var s) : Nat :=
  match x with
  | .free n => n
  | .bound x => env.lookup_var x

structure Retype (env1 : TypeEnv s1) (σ : Subst s1 s2) (env2 : TypeEnv s2) where
  var :
    ∀ (x : BVar s1 .var),
      env1.lookup_var x = interp_var env2 (σ.var x)

  tvar :
    ∀ (X : BVar s1 .tvar),
      env1.lookup_tvar X ≈ Ty.shape_val_denot env2 (σ.tvar X)

  cvar :
    ∀ (C : BVar s1 .cvar),
      env1.lookup_cvar C = (σ.cvar C).subst (Subst.from_TypeEnv env2)

lemma weaken_interp_var {x : Var .var s} :
  interp_var env x = interp_var (env.extend_var n) (x.rename Rename.succ) := by
  cases x <;> rfl

lemma tweaken_interp_var {x : Var .var s} :
  interp_var env x = interp_var (env.extend_tvar d) (x.rename Rename.succ) := by
  cases x <;> rfl

lemma cweaken_interp_var {cs : CaptureSet {}} {x : Var .var s} :
  interp_var env x = interp_var (env.extend_cvar cs) (x.rename Rename.succ) := by
  cases x <;> rfl

theorem Retype.liftVar
  {x : Nat}
  (ρ : Retype env1 σ env2) :
  Retype (env1.extend_var x) (σ.lift) (env2.extend_var x) where
  var := fun
    | .here => rfl
    | .there y => by
      change env1.lookup_var y = interp_var (env2.extend_var x) ((σ.var y).rename Rename.succ)
      conv =>
        rhs
        simp [<-weaken_interp_var]
      exact ρ.var y
  tvar := fun
    | .there X => by
      conv =>
        lhs
        simp [TypeEnv.extend_var, TypeEnv.lookup_tvar, TypeEnv.lookup]
      conv =>
        rhs
        simp [Subst.lift]
      apply IPreDenot.equiv_trans _ _ _ (ρ.tvar X)
      apply weaken_shape_val_denot
  cvar := fun
    | .there C => by
      simp only [List.empty_eq]
      change env1.lookup_cvar C = _
      rw [ρ.cvar C]
      apply rebind_resolved_capture_set Rebind.weaken

theorem Retype.liftTVar
  {d : IPreDenot}
  (ρ : Retype env1 σ env2) :
  Retype (env1.extend_tvar d) (σ.lift) (env2.extend_tvar d) where
  var := fun
    | .there x => by
      change env1.lookup_var x = interp_var (env2.extend_tvar d) ((σ.var x).rename Rename.succ)
      conv => rhs; simp [<-tweaken_interp_var]
      exact ρ.var x
  tvar := fun
    | .here => by
      conv => lhs; simp [TypeEnv.extend_tvar, TypeEnv.lookup_tvar, TypeEnv.lookup]
      conv =>
        rhs
        simp [Subst.lift, Ty.shape_val_denot]
        simp [TypeEnv.extend_tvar, TypeEnv.lookup_tvar, TypeEnv.lookup]
      apply IPreDenot.equiv_refl
    | .there X => by
      conv =>
        lhs
        simp [TypeEnv.extend_tvar, TypeEnv.lookup_tvar, TypeEnv.lookup]
      conv =>
        rhs
        simp [Subst.lift]
      apply IPreDenot.equiv_trans _ _ _ (ρ.tvar X)
      apply tweaken_shape_val_denot
  cvar := fun
    | .there C => by
      simp only [List.empty_eq]
      change env1.lookup_cvar C = _
      rw [ρ.cvar C]
      apply rebind_resolved_capture_set Rebind.tweaken

theorem Retype.liftCVar
  {cs : CaptureSet {}}
  (ρ : Retype env1 σ env2) :
  Retype (env1.extend_cvar cs) (σ.lift) (env2.extend_cvar cs) where
  var := fun
    | .there x => by
      change env1.lookup_var x = interp_var (env2.extend_cvar cs) ((σ.var x).rename Rename.succ)
      conv => rhs; simp [<-cweaken_interp_var]
      exact ρ.var x
  tvar := fun
    | .there X => by
      conv =>
        lhs
        simp [TypeEnv.extend_cvar, TypeEnv.lookup_tvar, TypeEnv.lookup]
      conv =>
        rhs
        simp [Subst.lift]
      apply IPreDenot.equiv_trans _ _ _ (ρ.tvar X)
      apply cweaken_shape_val_denot
  cvar := fun
    | .here => by
      simp [TypeEnv.extend_cvar, TypeEnv.lookup_cvar]
      rfl
    | .there C => by
      simp only [List.empty_eq]
      change env1.lookup_cvar C = _
      rw [ρ.cvar C]
      apply rebind_resolved_capture_set Rebind.cweaken

def retype_resolved_capture_set
  {s1 s2 : Sig} {env1 : TypeEnv s1} {σ : Subst s1 s2} {env2 : TypeEnv s2}
  (ρ : Retype env1 σ env2) (C : CaptureSet s1) :
  C.subst (Subst.from_TypeEnv env1) = (C.subst σ).subst (Subst.from_TypeEnv env2) := by
  induction C with
  | empty =>
      rfl
  | union C1 C2 ih1 ih2 =>
      simp only [CaptureSet.subst, ih1, ih2]
  | var x =>
      cases x with
      | bound x =>
          have hvar := ρ.var x
          cases hσ : σ.var x with
          | bound y =>
              rw [hσ] at hvar
              simp only [interp_var] at hvar
              simp only [CaptureSet.subst, Var.subst, Subst.from_TypeEnv, hσ]
              exact congrArg (fun n => CaptureSet.var (.free n)) hvar
          | free n =>
              rw [hσ] at hvar
              simp only [interp_var] at hvar
              simp only [CaptureSet.subst, Var.subst, Subst.from_TypeEnv, hσ]
              exact congrArg (fun m => CaptureSet.var (.free m)) hvar
      | free n =>
          simp only [CaptureSet.subst, Var.subst]
  | cvar C =>
      simpa only [CaptureSet.subst] using ρ.cvar C

def retype_captureset_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {σ : Subst s1 s2} {env2 : TypeEnv s2}
  (ρ : Retype env1 σ env2) (C : CaptureSet s1) :
  CaptureSet.denot env1 C = CaptureSet.denot env2 (C.subst σ) := by
  unfold CaptureSet.denot
  congr 1
  exact retype_resolved_capture_set ρ C

def retype_capturebound_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {σ : Subst s1 s2} {env2 : TypeEnv s2}
  (ρ : Retype env1 σ env2) (B : CaptureBound s1) :
  CaptureBound.denot env1 B = CaptureBound.denot env2 (B.subst σ) := by
  cases B
  case unbound => rfl
  case bound C =>
    simp only [CaptureBound.denot, CaptureBound.subst]
    funext m
    congr 1
    exact congrFun (retype_captureset_denot ρ C) m

mutual

def retype_shape_val_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {σ : Subst s1 s2} {env2 : TypeEnv s2}
  (ρ : Retype env1 σ env2) (T : Ty .shape s1) :
  Ty.shape_val_denot env1 T ≈ Ty.shape_val_denot env2 (T.subst σ) :=
  match T with
  | .top => IPreDenot.eq_to_equiv (by simp [Ty.shape_val_denot, Ty.subst])
  | .tvar X => by simpa only [Ty.shape_val_denot, Ty.subst] using ρ.tvar X
  | .unit => IPreDenot.eq_to_equiv (by simp [Ty.shape_val_denot, Ty.subst])
  | .cap => IPreDenot.eq_to_equiv (by simp [Ty.shape_val_denot, Ty.subst])
  | .bool => IPreDenot.eq_to_equiv (by simp [Ty.shape_val_denot, Ty.subst])
  | .cell T => by
    have ih := retype_capt_val_denot ρ T
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.subst]
    constructor
    · rintro ⟨l, n0, Rl, heq, hlk, hmem, hst, hbi⟩
      refine ⟨l, n0, Rl, heq, hlk, hmem, hst, ?_⟩
      intro j w' m' e'
      rw [hbi j w' m' e']
      exact ih j.val w' m' e'
    · rintro ⟨l, n0, Rl, heq, hlk, hmem, hst, hbi⟩
      refine ⟨l, n0, Rl, heq, hlk, hmem, hst, ?_⟩
      intro j w' m' e'
      rw [hbi j w' m' e']
      exact (ih j.val w' m' e').symm
  | .arrow T1 T2 => by
    have ih1 := retype_capt_val_denot ρ T1
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.subst]
    constructor
    · rintro ⟨hwf_e, cs, T0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs, T0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' arg hwle harg
      have ih2 := retype_exi_val_denot (ρ.liftVar (x := arg)) T2
      have harg' := (ih1 _ _ _ _).mpr harg
      exact (ExpDenot.equiv ih2).mp (hd j hjk st' m' arg hwle harg')
    · rintro ⟨hwf_e, cs, T0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs, T0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' arg hwle harg
      have ih2 := retype_exi_val_denot (ρ.liftVar (x := arg)) T2
      have harg' := (ih1 _ _ _ _).mp harg
      exact (ExpDenot.equiv ih2).mpr (hd j hjk st' m' arg hwle harg')
  | .poly T1 T2 => by
    have ih1 := retype_shape_val_denot ρ T1
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.subst]
    constructor
    · rintro ⟨hwf_e, cs0, S0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs0, S0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' denot hwle hproper himply
      have ih2 := retype_exi_val_denot (ρ.liftTVar (d := denot)) T2
      have himply' : denot.ImplyAfter j st' m' (Ty.shape_val_denot env1 T1) := by
        intro C i hij st'' m'' hwle' e' hdenot
        exact (ih1 C i st'' m'' e').mpr (himply C i hij st'' m'' hwle' e' hdenot)
      exact (ExpDenot.equiv ih2).mp (hd j hjk st' m' denot hwle hproper himply')
    · rintro ⟨hwf_e, cs0, S0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs0, S0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' denot hwle hproper himply
      have ih2 := retype_exi_val_denot (ρ.liftTVar (d := denot)) T2
      have himply' : denot.ImplyAfter j st' m' (Ty.shape_val_denot env2 (T1.subst σ)) := by
        intro C i hij st'' m'' hwle' e' hdenot
        exact (ih1 C i st'' m'' e').mp (himply C i hij st'' m'' hwle' e' hdenot)
      exact (ExpDenot.equiv ih2).mpr (hd j hjk st' m' denot hwle hproper himply')
  | .cpoly B T => by
    have hB := retype_capturebound_denot ρ B
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.subst, hB]
    constructor
    · rintro ⟨hwf_e, cs0, B0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs0, B0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' CS hwf hwle hsub_bound
      have ih2 := retype_exi_val_denot (ρ.liftCVar (cs := CS)) T
      exact (ExpDenot.equiv ih2).mp (hd j hjk st' m' CS hwf hwle hsub_bound)
    · rintro ⟨hwf_e, cs0, B0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs0, B0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' CS hwf hwle hsub_bound
      have ih2 := retype_exi_val_denot (ρ.liftCVar (cs := CS)) T
      exact (ExpDenot.equiv ih2).mpr (hd j hjk st' m' CS hwf hwle hsub_bound)

def retype_capt_val_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {σ : Subst s1 s2} {env2 : TypeEnv s2}
  (ρ : Retype env1 σ env2) (T : Ty .capt s1) :
  Ty.capt_val_denot env1 T ≈ Ty.capt_val_denot env2 (T.subst σ) :=
  match T with
  | .capt C S => by
    have hC := retype_captureset_denot ρ C
    have hS := retype_shape_val_denot ρ S
    intro k st m e
    simp only [Ty.capt_val_denot, Ty.subst]
    rw [← hC, ← retype_resolved_capture_set ρ C]
    constructor
    · rintro ⟨hsimple, hwf_e, hwf_C, hshape⟩
      exact ⟨hsimple, hwf_e, hwf_C, (hS (C.denot env1 m) k st m e).mp hshape⟩
    · rintro ⟨hsimple, hwf_e, hwf_C, hshape⟩
      exact ⟨hsimple, hwf_e, hwf_C, (hS (C.denot env1 m) k st m e).mpr hshape⟩

def retype_exi_val_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {σ : Subst s1 s2} {env2 : TypeEnv s2}
  (ρ : Retype env1 σ env2) (T : Ty .exi s1) :
  Ty.exi_val_denot env1 T ≈ Ty.exi_val_denot env2 (T.subst σ) :=
  match T with
  | .typ T => by
    have ih := retype_capt_val_denot ρ T
    intro k st m e
    simpa only [Ty.exi_val_denot, Ty.subst] using ih k st m e
  | .exi T => by
    intro k st m e
    simp only [Ty.exi_val_denot, Ty.subst]
    constructor
    · rintro ⟨CS, y, hres, hwf, hcapt⟩
      have ih := retype_capt_val_denot (ρ.liftCVar (cs := CS)) T
      exact ⟨CS, y, hres, hwf, (ih k st m (Exp.var y)).mp hcapt⟩
    · rintro ⟨CS, y, hres, hwf, hcapt⟩
      have ih := retype_capt_val_denot (ρ.liftCVar (cs := CS)) T
      exact ⟨CS, y, hres, hwf, (ih k st m (Exp.var y)).mpr hcapt⟩

end

def retype_capt_exp_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {σ : Subst s1 s2} {env2 : TypeEnv s2}
  (ρ : Retype env1 σ env2) (T : Ty .capt s1) :
  Ty.capt_exp_denot env1 T ≈ Ty.capt_exp_denot env2 (T.subst σ) := by
  have ih := retype_capt_val_denot ρ T
  intro A k st m e
  exact ExpDenot.equiv ih

def retype_exi_exp_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {σ : Subst s1 s2} {env2 : TypeEnv s2}
  (ρ : Retype env1 σ env2) (T : Ty .exi s1) :
  Ty.exi_exp_denot env1 T ≈ Ty.exi_exp_denot env2 (T.subst σ) := by
  have ih := retype_exi_val_denot ρ T
  intro A k st m e
  exact ExpDenot.equiv ih

def Retype.open_arg {env : TypeEnv s} {y : Var .var s} :
  Retype
    (env.extend_var (interp_var env y))
    (Subst.openVar y)
    env where
  var := fun x => by cases x <;> rfl
  tvar := fun
    | .there X => by
      change IPreDenot.Equiv (env.lookup_tvar X) _
      conv =>
        rhs
        simp [Subst.openVar]
      apply IPreDenot.eq_to_equiv
      simp [Ty.shape_val_denot, TypeEnv.lookup_tvar]
  cvar := fun
    | .there C => by
      change env.lookup_cvar C = _
      simp only [List.empty_eq]
      unfold TypeEnv.lookup_cvar
      rfl

theorem open_arg_shape_val_denot {env : TypeEnv s} {y : Var .var s} {T : Ty .shape (s,x)} :
  Ty.shape_val_denot (env.extend_var (interp_var env y)) T ≈
    Ty.shape_val_denot env (T.subst (Subst.openVar y)) := by
  apply retype_shape_val_denot Retype.open_arg

theorem open_arg_capt_val_denot {env : TypeEnv s} {y : Var .var s} {T : Ty .capt (s,x)} :
  Ty.capt_val_denot (env.extend_var (interp_var env y)) T ≈
    Ty.capt_val_denot env (T.subst (Subst.openVar y)) := by
  apply retype_capt_val_denot Retype.open_arg

theorem open_arg_exi_val_denot {env : TypeEnv s} {y : Var .var s} {T : Ty .exi (s,x)} :
  Ty.exi_val_denot (env.extend_var (interp_var env y)) T ≈
    Ty.exi_val_denot env (T.subst (Subst.openVar y)) := by
  apply retype_exi_val_denot Retype.open_arg

theorem open_arg_exi_exp_denot {env : TypeEnv s} {y : Var .var s} {T : Ty .exi (s,x)} :
  Ty.exi_exp_denot (env.extend_var (interp_var env y)) T ≈
    Ty.exi_exp_denot env (T.subst (Subst.openVar y)) := by
  apply retype_exi_exp_denot Retype.open_arg

def Retype.open_targ {env : TypeEnv s} {S : Ty .shape s} :
  Retype
    (env.extend_tvar (Ty.shape_val_denot env S))
    (Subst.openTVar S)
    env where
  var := fun x => by cases x; rfl
  tvar := fun
    | .here => by
      apply IPreDenot.eq_to_equiv
      rfl
    | .there X => by
      apply IPreDenot.eq_to_equiv
      simp [TypeEnv.extend_tvar, TypeEnv.lookup_tvar]
      simp [Subst.openTVar, Ty.shape_val_denot]
      rfl
  cvar := fun
    | .there C => by
      simp only [List.empty_eq]
      unfold TypeEnv.lookup_cvar
      rfl

theorem open_targ_shape_val_denot {env : TypeEnv s} {S : Ty .shape s} {T : Ty .shape (s,X)} :
  Ty.shape_val_denot (env.extend_tvar (Ty.shape_val_denot env S)) T ≈
    Ty.shape_val_denot env (T.subst (Subst.openTVar S)) := by
  apply retype_shape_val_denot Retype.open_targ

theorem open_targ_capt_val_denot {env : TypeEnv s} {S : Ty .shape s} {T : Ty .capt (s,X)} :
  Ty.capt_val_denot (env.extend_tvar (Ty.shape_val_denot env S)) T ≈
    Ty.capt_val_denot env (T.subst (Subst.openTVar S)) := by
  apply retype_capt_val_denot Retype.open_targ

theorem open_targ_exi_val_denot {env : TypeEnv s} {S : Ty .shape s} {T : Ty .exi (s,X)} :
  Ty.exi_val_denot (env.extend_tvar (Ty.shape_val_denot env S)) T ≈
    Ty.exi_val_denot env (T.subst (Subst.openTVar S)) := by
  apply retype_exi_val_denot Retype.open_targ

theorem open_targ_exi_exp_denot {env : TypeEnv s} {S : Ty .shape s} {T : Ty .exi (s,X)} :
  Ty.exi_exp_denot (env.extend_tvar (Ty.shape_val_denot env S)) T ≈
    Ty.exi_exp_denot env (T.subst (Subst.openTVar S)) := by
  apply retype_exi_exp_denot Retype.open_targ

def Retype.open_carg {env : TypeEnv s} {C : CaptureSet s} :
  Retype
    (env.extend_cvar (C.subst (Subst.from_TypeEnv env)))
    (Subst.openCVar C)
    env where
  var := fun x => by cases x; rfl
  tvar := fun
    | .there X => by
      change IPreDenot.Equiv (env.lookup_tvar X) _
      apply IPreDenot.eq_to_equiv
      simp [TypeEnv.lookup_tvar, Subst.openCVar, Ty.shape_val_denot]
  cvar := fun
    | .here => by
      change C.subst (Subst.from_TypeEnv env) = _
      simp [Subst.openCVar]
    | .there C => by
      change env.lookup_cvar C = _
      simp [Subst.openCVar, TypeEnv.lookup_cvar, CaptureSet.subst, Subst.from_TypeEnv]

theorem open_carg_shape_val_denot {env : TypeEnv s} {C : CaptureSet s} {T : Ty .shape (s,C)} :
  Ty.shape_val_denot (env.extend_cvar (C.subst (Subst.from_TypeEnv env))) T ≈
    Ty.shape_val_denot env (T.subst (Subst.openCVar C)) := by
  apply retype_shape_val_denot Retype.open_carg

theorem open_carg_capt_val_denot {env : TypeEnv s} {C : CaptureSet s} {T : Ty .capt (s,C)} :
  Ty.capt_val_denot (env.extend_cvar (C.subst (Subst.from_TypeEnv env))) T ≈
    Ty.capt_val_denot env (T.subst (Subst.openCVar C)) := by
  apply retype_capt_val_denot Retype.open_carg

theorem open_carg_exi_val_denot {env : TypeEnv s} {C : CaptureSet s} {T : Ty .exi (s,C)} :
  Ty.exi_val_denot (env.extend_cvar (C.subst (Subst.from_TypeEnv env))) T ≈
    Ty.exi_val_denot env (T.subst (Subst.openCVar C)) := by
  apply retype_exi_val_denot Retype.open_carg

theorem open_carg_exi_exp_denot {env : TypeEnv s} {C : CaptureSet s} {T : Ty .exi (s,C)} :
  Ty.exi_exp_denot (env.extend_cvar (C.subst (Subst.from_TypeEnv env))) T ≈
    Ty.exi_exp_denot env (T.subst (Subst.openCVar C)) := by
  apply retype_exi_exp_denot Retype.open_carg

end CC
