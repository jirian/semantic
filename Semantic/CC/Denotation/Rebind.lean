import Semantic.CC.Denotation.Core
namespace CC

structure Rebind (env1 : TypeEnv s1) (f : Rename s1 s2) (env2 : TypeEnv s2) : Prop where
  var :
    ∀ (x : BVar s1 k),
      env1.lookup x = env2.lookup (f.var x)

def Rebind.liftVar
  (ρ : Rebind env1 f env2) :
  Rebind (env1.extend_var x) (f.lift) (env2.extend_var x) where
  var := fun
    | .here => rfl
    | .there y => by
      change env1.lookup y = env2.lookup (f.var y)
      exact ρ.var y

def Rebind.liftTVar
  (ρ : Rebind env1 f env2) :
  Rebind (env1.extend_tvar d) (f.lift) (env2.extend_tvar d) where
  var := fun
    | .here => rfl
    | .there y => by
      change env1.lookup y = env2.lookup (f.var y)
      exact ρ.var y

def Rebind.liftCVar
  (ρ : Rebind env1 f env2) (cs : CaptureSet {}) :
  Rebind (env1.extend_cvar cs) (f.lift) (env2.extend_cvar cs) where
  var := fun
    | .here => rfl
    | .there y => by
      change env1.lookup y = env2.lookup (f.var y)
      exact ρ.var y

theorem rebind_resolved_capture_set {C : CaptureSet s1}
  (ρ : Rebind env1 f env2) :
  C.subst (Subst.from_TypeEnv env1) =
    (C.rename f).subst (Subst.from_TypeEnv env2) := by
  induction C with
  | empty =>
    simp [CaptureSet.subst, CaptureSet.rename]
  | union C1 C2 ih1 ih2 =>
    simp [CaptureSet.subst, CaptureSet.rename, ih1, ih2]
  | var x =>
    cases x with
    | free n =>
      simp [CaptureSet.subst, CaptureSet.rename, Var.subst, Var.rename]
    | bound x =>
      have h := ρ.var x
      cases k : env1.lookup x with
      | var n =>
        rw [k] at h
        simp only [CaptureSet.subst, CaptureSet.rename, Var.subst, Var.rename,
          Subst.from_TypeEnv, TypeEnv.lookup_var, k]
        rw [← h]
  | cvar x =>
    have h := ρ.var x
    cases k1 : env1.lookup x with
    | cvar cs1 =>
      rw [k1] at h
      cases k2 : env2.lookup (f.var x) with
      | cvar cs2 =>
        rw [k2] at h
        cases h
        simp only [CaptureSet.subst, CaptureSet.rename, Subst.from_TypeEnv,
          TypeEnv.lookup_cvar, k1, k2]

/- Rebinding for CaptureSet.denot -/
theorem rebind_captureset_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {f : Rename s1 s2} {env2 : TypeEnv s2}
  (ρ : Rebind env1 f env2) (C : CaptureSet s1) :
  CaptureSet.denot env1 C = CaptureSet.denot env2 (C.rename f) := by
  unfold CaptureSet.denot
  congr 1
  exact rebind_resolved_capture_set ρ

theorem rebind_capturebound_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {f : Rename s1 s2} {env2 : TypeEnv s2}
  (ρ : Rebind env1 f env2) (B : CaptureBound s1) :
  CaptureBound.denot env1 B = CaptureBound.denot env2 (B.rename f) := by
  cases B
  case unbound => rfl
  case bound C =>
    funext m
    change CapabilityBound.set (CaptureSet.denot env1 C m) =
      CapabilityBound.set (CaptureSet.denot env2 (C.rename f) m)
    exact congrArg CapabilityBound.set (congrFun (rebind_captureset_denot ρ C) m)

mutual

def rebind_shape_val_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {f : Rename s1 s2} {env2 : TypeEnv s2}
  (ρ : Rebind env1 f env2) (T : Ty .shape s1) :
  Ty.shape_val_denot env1 T ≈ Ty.shape_val_denot env2 (T.rename f) :=
  match T with
  | .top => by
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.rename]
  | .tvar X => by
    apply IPreDenot.eq_to_equiv
    have h := ρ.var X
    cases k : env1.lookup X
    case tvar d =>
      simp [k] at h
      simp only [Ty.shape_val_denot, Ty.rename, TypeEnv.lookup_tvar, k, h]
  | .unit => by
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.rename]
  | .cap => by
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.rename]
  | .bool => by
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.rename]
  | .cell T => by
    have ih := rebind_capt_val_denot ρ T
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.rename]
    constructor
    · rintro ⟨l, n0, Rl, heq, hlk, hmem, hst, hbi⟩
      refine ⟨l, n0, Rl, heq, hlk, hmem, hst, ?_⟩
      intro j w' m' e'
      rw [hbi j w' m' e']
      exact ih j.val w' m' e'
    · rintro ⟨l, n0, Rl, heq, hlk, hmem, hst, hbi⟩
      refine ⟨l, n0, Rl, heq, hlk, hmem, hst, ?_⟩
      intro j w' m' e'
      rw [hbi j w' m' e']
      exact (ih j.val w' m' e').symm
  | .arrow T1 T2 => by
    have ih1 := rebind_capt_val_denot ρ T1
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.rename]
    constructor
    · rintro ⟨hwf_e, cs, T0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs, T0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' arg hwle harg
      have ih2 := rebind_exi_val_denot (ρ.liftVar (x := arg)) T2
      have harg' := (ih1 _ _ _ _).mpr harg
      exact (ExpDenot.equiv ih2).mp (hd j hjk st' m' arg hwle harg')
    · rintro ⟨hwf_e, cs, T0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs, T0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' arg hwle harg
      have ih2 := rebind_exi_val_denot (ρ.liftVar (x := arg)) T2
      have harg' := (ih1 _ _ _ _).mp harg
      exact (ExpDenot.equiv ih2).mpr (hd j hjk st' m' arg hwle harg')
  | .poly T1 T2 => by
    have ih1 := rebind_shape_val_denot ρ T1
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.rename]
    constructor
    · rintro ⟨hwf_e, cs0, S0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs0, S0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' denot hwle hproper himply
      have ih2 := rebind_exi_val_denot (ρ.liftTVar (d := denot)) T2
      have himply' : denot.ImplyAfter j st' m' (Ty.shape_val_denot env1 T1) := by
        intro C i hij st'' m'' hwle' e' hdenot
        exact (ih1 C i st'' m'' e').mpr (himply C i hij st'' m'' hwle' e' hdenot)
      exact (ExpDenot.equiv ih2).mp (hd j hjk st' m' denot hwle hproper himply')
    · rintro ⟨hwf_e, cs0, S0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs0, S0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' denot hwle hproper himply
      have ih2 := rebind_exi_val_denot (ρ.liftTVar (d := denot)) T2
      have himply' : denot.ImplyAfter j st' m' (Ty.shape_val_denot env2 (T1.rename f)) := by
        intro C i hij st'' m'' hwle' e' hdenot
        exact (ih1 C i st'' m'' e').mp (himply C i hij st'' m'' hwle' e' hdenot)
      exact (ExpDenot.equiv ih2).mpr (hd j hjk st' m' denot hwle hproper himply')
  | .cpoly B T => by
    have hB := rebind_capturebound_denot ρ B
    intro A k st m e
    simp only [Ty.shape_val_denot, Ty.rename, hB]
    constructor
    · rintro ⟨hwf_e, cs0, B0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs0, B0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' CS hwf hwle hsub_bound
      have ih2 := rebind_exi_val_denot (ρ.liftCVar CS) T
      exact (ExpDenot.equiv ih2).mp (hd j hjk st' m' CS hwf hwle hsub_bound)
    · rintro ⟨hwf_e, cs0, B0, t0, hr, hwf, hR0_sub, hd⟩
      refine ⟨hwf_e, cs0, B0, t0, hr, hwf, hR0_sub, ?_⟩
      intro j hjk st' m' CS hwf hwle hsub_bound
      have ih2 := rebind_exi_val_denot (ρ.liftCVar CS) T
      exact (ExpDenot.equiv ih2).mpr (hd j hjk st' m' CS hwf hwle hsub_bound)

def rebind_capt_val_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {f : Rename s1 s2} {env2 : TypeEnv s2}
  (ρ : Rebind env1 f env2) (T : Ty .capt s1) :
  Ty.capt_val_denot env1 T ≈ Ty.capt_val_denot env2 (T.rename f) :=
  match T with
  | .capt C S => by
    have hC := rebind_captureset_denot ρ C
    have hS := rebind_shape_val_denot ρ S
    intro k st m e
    simp only [Ty.capt_val_denot, Ty.rename]
    rw [← hC]
    constructor
    · rintro ⟨hsimple, hwf_e, hwf_C, hshape⟩
      refine ⟨hsimple, hwf_e, ?_, ?_⟩
      · rw [← rebind_resolved_capture_set ρ]; exact hwf_C
      · exact (hS (C.denot env1 m) k st m e).mp hshape
    · rintro ⟨hsimple, hwf_e, hwf_C, hshape⟩
      refine ⟨hsimple, hwf_e, ?_, ?_⟩
      · rw [rebind_resolved_capture_set ρ]; exact hwf_C
      · exact (hS (C.denot env1 m) k st m e).mpr hshape

def rebind_exi_val_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {f : Rename s1 s2} {env2 : TypeEnv s2}
  (ρ : Rebind env1 f env2) (T : Ty .exi s1) :
  Ty.exi_val_denot env1 T ≈ Ty.exi_val_denot env2 (T.rename f) :=
  match T with
  | .typ T => by
    have ih := rebind_capt_val_denot ρ T
    intro k st m e
    simpa only [Ty.exi_val_denot, Ty.rename] using ih k st m e
  | .exi T => by
    intro k st m e
    simp only [Ty.exi_val_denot, Ty.rename]
    constructor
    · rintro ⟨CS, y, hres, hwf, hcapt⟩
      have ih := rebind_capt_val_denot (ρ.liftCVar CS) T
      exact ⟨CS, y, hres, hwf, (ih k st m (Exp.var y)).mp hcapt⟩
    · rintro ⟨CS, y, hres, hwf, hcapt⟩
      have ih := rebind_capt_val_denot (ρ.liftCVar CS) T
      exact ⟨CS, y, hres, hwf, (ih k st m (Exp.var y)).mpr hcapt⟩

end

def rebind_capt_exp_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {f : Rename s1 s2} {env2 : TypeEnv s2}
  (ρ : Rebind env1 f env2) (T : Ty .capt s1) :
  Ty.capt_exp_denot env1 T ≈ Ty.capt_exp_denot env2 (T.rename f) := by
  have ih := rebind_capt_val_denot ρ T
  intro A k st m e
  exact ExpDenot.equiv ih

def rebind_exi_exp_denot
  {s1 s2 : Sig} {env1 : TypeEnv s1} {f : Rename s1 s2} {env2 : TypeEnv s2}
  (ρ : Rebind env1 f env2) (T : Ty .exi s1) :
  Ty.exi_exp_denot env1 T ≈ Ty.exi_exp_denot env2 (T.rename f) := by
  have ih := rebind_exi_val_denot ρ T
  intro A k st m e
  exact ExpDenot.equiv ih

def Rebind.weaken {env : TypeEnv s} {x : Nat} :
  Rebind env Rename.succ (env.extend_var x) where
  var := fun _ => rfl

def Rebind.tweaken {env : TypeEnv s} {d : IPreDenot} :
  Rebind env Rename.succ (env.extend_tvar d) where
  var := fun _ => rfl

def Rebind.cweaken {env : TypeEnv s} {cs : CaptureSet {}} :
  Rebind env Rename.succ (env.extend_cvar cs) where
  var := fun _ => rfl

lemma weaken_shape_val_denot {env : TypeEnv s} {T : Ty .shape s} :
  Ty.shape_val_denot env T ≈ Ty.shape_val_denot (env.extend_var x) (T.rename Rename.succ) := by
  apply rebind_shape_val_denot (ρ:=Rebind.weaken) (T:=T)

lemma weaken_capt_val_denot {env : TypeEnv s} {T : Ty .capt s} :
  Ty.capt_val_denot env T ≈ Ty.capt_val_denot (env.extend_var x) (T.rename Rename.succ) := by
  apply rebind_capt_val_denot (ρ:=Rebind.weaken) (T:=T)

lemma weaken_exi_val_denot {env : TypeEnv s} {T : Ty .exi s} :
  Ty.exi_val_denot env T ≈ Ty.exi_val_denot (env.extend_var x) (T.rename Rename.succ) := by
  apply rebind_exi_val_denot (ρ:=Rebind.weaken) (T:=T)

lemma tweaken_shape_val_denot {env : TypeEnv s} {T : Ty .shape s} :
  Ty.shape_val_denot env T ≈ Ty.shape_val_denot (env.extend_tvar d) (T.rename Rename.succ) := by
  apply rebind_shape_val_denot (ρ:=Rebind.tweaken) (T:=T)

lemma tweaken_capt_val_denot {env : TypeEnv s} {T : Ty .capt s} :
  Ty.capt_val_denot env T ≈ Ty.capt_val_denot (env.extend_tvar d) (T.rename Rename.succ) := by
  apply rebind_capt_val_denot (ρ:=Rebind.tweaken) (T:=T)

lemma tweaken_exi_val_denot {env : TypeEnv s} {T : Ty .exi s} :
  Ty.exi_val_denot env T ≈ Ty.exi_val_denot (env.extend_tvar d) (T.rename Rename.succ) := by
  apply rebind_exi_val_denot (ρ:=Rebind.tweaken) (T:=T)

lemma cweaken_shape_val_denot {env : TypeEnv s} {cs : CaptureSet {}}
  {T : Ty .shape s} :
  Ty.shape_val_denot env T ≈
    Ty.shape_val_denot (env.extend_cvar cs) (T.rename Rename.succ) := by
  apply rebind_shape_val_denot (ρ:=Rebind.cweaken) (T:=T)

lemma cweaken_capt_val_denot {env : TypeEnv s} {cs : CaptureSet {}}
  {T : Ty .capt s} :
  Ty.capt_val_denot env T ≈
    Ty.capt_val_denot (env.extend_cvar cs) (T.rename Rename.succ) := by
  apply rebind_capt_val_denot (ρ:=Rebind.cweaken) (T:=T)

lemma cweaken_exi_val_denot {env : TypeEnv s} {cs : CaptureSet {}}
  {T : Ty .exi s} :
  Ty.exi_val_denot env T ≈
    Ty.exi_val_denot (env.extend_cvar cs) (T.rename Rename.succ) := by
  apply rebind_exi_val_denot (ρ:=Rebind.cweaken) (T:=T)

end CC
