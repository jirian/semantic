import Semantic.CoreCapybara.Syntax.Ty

/-!
Expression definitions and operations for CC.
-/

namespace CoreCapybara

/-- An expression in CC. -/
inductive Exp : Sig -> Type where
| var : Var .var s -> Exp s
| abs : CaptureSet s -> Ty .capt s -> Exp (s,x) -> Exp s
| tabs : CaptureSet s -> PureTy s -> Exp (s,X) -> Exp s
| cabs : CaptureSet s -> CaptureBound s -> Exp (s,C) -> Exp s
| consumer : CaptureSet s -> Ty .exi s -> Exp (s,C,x) -> Exp s
| boxed : CaptureSet s -> ModalCtx s -> Exp s -> Exp s
| reader : Var .var s -> Exp s
| alloc : Var .var s -> Exp s
| drop : Var .var s -> Exp s
| pack : List.Vector (CaptureSet s) n -> Var .var s -> Exp s
| app : Var .var s -> Var .var s -> Exp s
| tapp : Var .var s -> PureTy s -> Exp s
| capp : Var .var s -> CaptureSet s -> Exp s
| consumer_app : Var .var s -> Exp s -> Exp s
| unwrap : Var .var s -> Exp s
| letin : Exp s -> Exp (s,x) -> Exp s
| unpack : (n : Nat) -> Exp s -> Exp ((s.extendCVars n),x) -> Exp s
| unit : Exp s
| btrue : Exp s
| bfalse : Exp s
| nat : Nat -> Exp s
| read : Var .var s -> Exp s
| write : Var .var s -> Var .var s -> Exp s
| cond : Var .var s -> Exp s -> Exp s -> Exp s
| par :
  CaptureSet s -> CaptureSet s ->
  Exp s -> Exp s ->
  Exp s
/-- An array value: the list of its (distinct) cells. -/
| arr : List (Var .var s) -> Exp s
/-- `idx a n d`: the `n`-th cell of array `a`, or the fallback cell `d` if out of range. -/
| idx : Var .var s -> Nat -> Var .var s -> Exp s
/-- `concat a b`: joins two separated arrays. -/
| concat : Var .var s -> Var .var s -> Exp s
/-- `split a i`: consumes `a` and splits it at the position held by `i` into two fresh arrays. -/
| split : Var .var s -> Var .var s -> Exp s
/-- A pair of variables. -/
| pair : Var .var s -> Var .var s -> Exp s
/-- First projection. -/
| fst : Var .var s -> Exp s
/-- Second projection. -/
| snd : Var .var s -> Exp s

/-- Applies a renaming to all bound variables in an expression. -/
def Exp.rename : Exp s1 -> Rename s1 s2 -> Exp s2
| .var x, f => .var (x.rename f)
| .abs cs T e, f => .abs (cs.rename f) (T.rename f) (e.rename (f.lift))
| .tabs cs T e, f => .tabs (cs.rename f) (T.rename f) (e.rename (f.lift))
| .cabs cs cb e, f => .cabs (cs.rename f) (cb.rename f) (e.rename (f.lift))
| .consumer cs T e, f => .consumer (cs.rename f) (T.rename f) (e.rename (f.lift.lift))
| .boxed cs Ψ e, f => .boxed (cs.rename f) (Ψ.rename f) (e.rename f)
| .reader x, f => .reader (x.rename f)
| .alloc x, f => .alloc (x.rename f)
| .drop x, f => .drop (x.rename f)
| .pack css x, f => .pack (css.map (·.rename f)) (x.rename f)
-- NB: `css.map` here is `List.Vector.map`, preserving the length index `n`.
| .app x y, f => .app (x.rename f) (y.rename f)
| .tapp x T, f => .tapp (x.rename f) (T.rename f)
| .capp x cs, f => .capp (x.rename f) (cs.rename f)
| .consumer_app x e, f => .consumer_app (x.rename f) (e.rename f)
| .unwrap x, f => .unwrap (x.rename f)
| .letin e1 e2, f => .letin (e1.rename f) (e2.rename (f.lift))
| .unpack n e1 e2, f => .unpack n (e1.rename f) (e2.rename ((f.liftCVars n).lift))
| .unit, _ => .unit
| .btrue, _ => .btrue
| .bfalse, _ => .bfalse
| .nat n, _ => .nat n
| .read x, f => .read (x.rename f)
| .write x y, f => .write (x.rename f) (y.rename f)
| .cond x e2 e3, f => .cond (x.rename f) (e2.rename f) (e3.rename f)
| .par C1 C2 e1 e2, f => .par (C1.rename f) (C2.rename f) (e1.rename f) (e2.rename f)
| .arr xs, f => .arr (xs.map (·.rename f))
| .idx x n d, f => .idx (x.rename f) n (d.rename f)
| .concat x y, f => .concat (x.rename f) (y.rename f)
| .split x y, f => .split (x.rename f) (y.rename f)
| .pair x y, f => .pair (x.rename f) (y.rename f)
| .fst x, f => .fst (x.rename f)
| .snd x, f => .snd (x.rename f)

/-- An expression is a value if it is an abstraction, pack, or unit. -/
inductive Exp.IsVal : Exp s -> Prop where
| abs : Exp.IsVal (.abs cs T e)
| tabs : Exp.IsVal (.tabs cs T e)
| cabs : Exp.IsVal (.cabs cs m e)
| consumer : Exp.IsVal (.consumer cs T e)
| boxed : Exp.IsVal (.boxed cs Ψ e)
| pack : Exp.IsVal (.pack css x)
| reader : Exp.IsVal (.reader x)
| unit : Exp.IsVal .unit
| btrue : Exp.IsVal .btrue
| bfalse : Exp.IsVal .bfalse
| nat : Exp.IsVal (.nat n)
| arr : Exp.IsVal (.arr xs)
| pair : Exp.IsVal (.pair x y)

/-- A simple value is a value that is not a pack. Therefore,
      a simple value always has a capturing type, not an existential type. -/
inductive Exp.IsSimpleVal : Exp s -> Prop where
| abs : Exp.IsSimpleVal (.abs cs T e)
| tabs : Exp.IsSimpleVal (.tabs cs T e)
| cabs : Exp.IsSimpleVal (.cabs cs m e)
| consumer : Exp.IsSimpleVal (.consumer cs T e)
| boxed : Exp.IsSimpleVal (.boxed cs Ψ e)
| unit : Exp.IsSimpleVal .unit
| btrue : Exp.IsSimpleVal .btrue
| bfalse : Exp.IsSimpleVal .bfalse
| nat : Exp.IsSimpleVal (.nat n)
| reader : Exp.IsSimpleVal (.reader x)
| arr : Exp.IsSimpleVal (.arr xs)
| pair : Exp.IsSimpleVal (.pair x y)

inductive Exp.IsSimpleAns : Exp s -> Prop where
| is_simple_val :
  (hv : Exp.IsSimpleVal v) ->
  Exp.IsSimpleAns v
| is_var :
  Exp.IsSimpleAns (.var x)

/-- `e` is a `pack` value of arity exactly `n`.  The arity index is essential for
    progress: `unpack n` only fires on a pack whose evidence vector has length `n`. -/
inductive Exp.IsPack {s : Sig} (n : Nat) : Exp s -> Prop where
| pack {css : List.Vector (CaptureSet s) n} {x : Var .var s} : Exp.IsPack n (.pack css x)

/-- A value, bundling an expression with a proof that it is a value. -/
structure Val (s : Sig) where
  unwrap : Exp s
  isVal : unwrap.IsVal

/-- Renaming by the identity renaming leaves a variable unchanged. -/
def Var.rename_id {x : Var k s} : x.rename (Rename.id) = x := by
  cases x <;> rfl

/-- Renaming by the identity renaming leaves an expression unchanged. -/
def Exp.rename_id {e : Exp s} : e.rename (Rename.id) = e := by
  induction e with
  | var x =>
    simp only [Exp.rename, Var.rename_id]
  | abs cs T e ih =>
    simp only [Exp.rename, CaptureSet.rename_id, Ty.rename_id, Rename.lift_id]
    exact congrArg (Exp.abs cs T) ih
  | tabs cs T e ih =>
    simp only [Exp.rename, CaptureSet.rename_id, PureTy.rename_id, Rename.lift_id]
    exact congrArg (Exp.tabs cs T) ih
  | cabs cs cb e ih =>
    simp only [Exp.rename, CaptureSet.rename_id, CaptureBound.rename_id, Rename.lift_id]
    exact congrArg (Exp.cabs cs cb) ih
  | consumer cs T e ih =>
    simp only [Exp.rename, CaptureSet.rename_id, Ty.rename_id, Rename.lift_id]
    exact congrArg (Exp.consumer cs T) ih
  | boxed cs Ψ e ih =>
    simp only [Exp.rename, CaptureSet.rename_id, ModalCtx.rename_id]
    exact congrArg (Exp.boxed cs Ψ) ih
  | reader x =>
    simp only [Exp.rename, Var.rename_id]
  | alloc x =>
    simp only [Exp.rename, Var.rename_id]
  | drop x =>
    simp only [Exp.rename, Var.rename_id]
  | pack css x =>
    simp only [Exp.rename, Var.rename_id]
    congr 1
    apply List.Vector.toList_injective
    simp only [List.Vector.toList_map, CaptureSet.rename_id, List.map_id']
  | app x y =>
    simp only [Exp.rename, Var.rename_id]
  | tapp x T =>
    simp only [Exp.rename, Var.rename_id, PureTy.rename_id]
  | capp x cs =>
    simp only [Exp.rename, Var.rename_id, CaptureSet.rename_id]
  | consumer_app x e ih =>
    simp only [Exp.rename, Var.rename_id, ih]
  | unwrap x =>
    simp only [Exp.rename, Var.rename_id]
  | letin e1 e2 ih1 ih2 =>
    simp only [Exp.rename, Rename.lift_id, ih1]
    exact congrArg (Exp.letin e1) ih2
  | unpack n e1 e2 ih1 ih2 =>
    simp only [Exp.rename, Rename.liftCVars_id, Rename.lift_id, ih1]
    exact congrArg (Exp.unpack n e1) ih2
  | unit => rfl
  | btrue => rfl
  | bfalse => rfl
  | nat _ => rfl
  | read x =>
    simp only [Exp.rename, Var.rename_id]
  | write x y =>
    simp only [Exp.rename, Var.rename_id]
  | cond x e2 e3 ih2 ih3 =>
    simp only [Exp.rename, Var.rename_id, ih2]
    exact congrArg (Exp.cond x e2) ih3
  | par C1 C2 e1 e2 ih1 ih2 =>
    simp only [Exp.rename, CaptureSet.rename_id, ih1, ih2]
  | arr xs =>
    simp only [Exp.rename, Var.rename_id, List.map_id']
  | idx x n d =>
    simp only [Exp.rename, Var.rename_id]
  | concat x y =>
    simp only [Exp.rename, Var.rename_id]
  | split x y =>
    simp only [Exp.rename, Var.rename_id]
  | pair x y =>
    simp only [Exp.rename, Var.rename_id]
  | fst x =>
    simp only [Exp.rename, Var.rename_id]
  | snd x =>
    simp only [Exp.rename, Var.rename_id]

/-- Renaming distributes over composition of renamings. -/
theorem Var.rename_comp {x : Var k s1} {f : Rename s1 s2} {g : Rename s2 s3} :
    (x.rename f).rename g = x.rename (f.comp g) := by
  cases x <;> rfl

/-- Renaming distributes over composition of renamings. -/
theorem Exp.rename_comp {e : Exp s1} {f : Rename s1 s2} {g : Rename s2 s3} :
    (e.rename f).rename g = e.rename (f.comp g) := by
  induction e generalizing s2 s3 with
  | var x =>
    simp only [Exp.rename, Var.rename_comp]
  | abs cs T e ih =>
    simpa only [Exp.rename, CaptureSet.rename_comp, Ty.rename_comp, Rename.lift_comp] using
      congrArg (Exp.abs (cs.rename (f.comp g)) (T.rename (f.comp g)))
        (ih (f := f.lift) (g := g.lift))
  | tabs cs T e ih =>
    simpa only [Exp.rename, CaptureSet.rename_comp, PureTy.rename_comp, Rename.lift_comp] using
      congrArg (Exp.tabs (cs.rename (f.comp g)) (T.rename (f.comp g)))
        (ih (f := f.lift) (g := g.lift))
  | cabs cs cb e ih =>
    simpa only [
      Exp.rename,
      CaptureSet.rename_comp,
      CaptureBound.rename_comp,
      Rename.lift_comp
    ] using
      congrArg (Exp.cabs (cs.rename (f.comp g)) (cb.rename (f.comp g)))
        (ih (f := f.lift) (g := g.lift))
  | consumer cs T e ih =>
    simpa only [
      Exp.rename,
      CaptureSet.rename_comp,
      Ty.rename_comp,
      Rename.lift_comp
    ] using
      congrArg (Exp.consumer (cs.rename (f.comp g)) (T.rename (f.comp g)))
        (ih (f := f.lift.lift) (g := g.lift.lift))
  | boxed cs Ψ e ih =>
    simpa only [Exp.rename, CaptureSet.rename_comp, ModalCtx.rename_comp] using
      congrArg (Exp.boxed (cs.rename (f.comp g)) (Ψ.rename (f.comp g))) (ih (f := f) (g := g))
  | reader x =>
    simp only [Exp.rename, Var.rename_comp]
  | alloc x =>
    simp only [Exp.rename, Var.rename_comp]
  | drop x =>
    simp only [Exp.rename, Var.rename_comp]
  | pack css x =>
    simp only [Exp.rename, Var.rename_comp]
    congr 1
    apply List.Vector.toList_injective
    simp only [List.Vector.toList_map, List.map_map, Function.comp_def, CaptureSet.rename_comp]
  | app x y =>
    simp only [Exp.rename, Var.rename_comp]
  | tapp x T =>
    simp only [Exp.rename, Var.rename_comp, PureTy.rename_comp]
  | capp x cs =>
    simp only [Exp.rename, Var.rename_comp, CaptureSet.rename_comp]
  | consumer_app x e ih =>
    simp only [Exp.rename, Var.rename_comp, ih]
  | unwrap x =>
    simp only [Exp.rename, Var.rename_comp]
  | letin e1 e2 ih1 ih2 =>
    simpa only [Exp.rename, Rename.lift_comp, ih1] using
      congrArg (Exp.letin (e1.rename (f.comp g))) (ih2 (f := f.lift) (g := g.lift))
  | unpack n e1 e2 ih1 ih2 =>
    simpa only [Exp.rename, Rename.liftCVars_comp, Rename.lift_comp, ih1] using
      congrArg (Exp.unpack n (e1.rename (f.comp g)))
        (ih2 (f := (f.liftCVars n).lift) (g := (g.liftCVars n).lift))
  | unit => rfl
  | btrue => rfl
  | bfalse => rfl
  | nat _ => rfl
  | read x =>
    simp only [Exp.rename, Var.rename_comp]
  | write x y =>
    simp only [Exp.rename, Var.rename_comp]
  | cond x e2 e3 ih2 ih3 =>
    simpa only [Exp.rename, Var.rename_comp, ih2] using
      congrArg (Exp.cond (x.rename (f.comp g)) (e2.rename (f.comp g))) (ih3 (f := f) (g := g))
  | par C1 C2 e1 e2 ih1 ih2 =>
    simp only [Exp.rename, CaptureSet.rename_comp, ih1, ih2]
  | arr xs =>
    simp only [Exp.rename, List.map_map, Function.comp_def, Var.rename_comp]
  | idx x n d =>
    simp only [Exp.rename, Var.rename_comp]
  | concat x y =>
    simp only [Exp.rename, Var.rename_comp]
  | split x y =>
    simp only [Exp.rename, Var.rename_comp]
  | pair x y =>
    simp only [Exp.rename, Var.rename_comp]
  | fst x =>
    simp only [Exp.rename, Var.rename_comp]
  | snd x =>
    simp only [Exp.rename, Var.rename_comp]

/-- Weakening commutes with renaming under a binder. -/
theorem Var.weaken_rename_comm {x : Var k s1} {f : Rename s1 s2} :
    (x.rename Rename.succ).rename (f.lift (k:=k0)) = (x.rename f).rename (Rename.succ) := by
  simp [Var.rename_comp, Rename.succ_lift_comm]

/-- An answer is a value or a free variable in the empty context. -/
inductive Exp.IsAns : Exp {} -> Prop where
| is_val :
  (hv : Exp.IsVal v) ->
  Exp.IsAns v
| is_var :
  Exp.IsAns (.var x)

/-- An expression is closed if it contains no heap pointers. -/
inductive Exp.IsClosed : Exp s -> Prop where
| var : Var.IsClosed x -> Exp.IsClosed (.var x)
| abs : CaptureSet.IsClosed cs -> Ty.IsClosed T -> Exp.IsClosed e ->
    Exp.IsClosed (.abs cs T e)
| tabs : CaptureSet.IsClosed cs -> PureTy.IsClosed T -> Exp.IsClosed e ->
    Exp.IsClosed (.tabs cs T e)
| cabs : CaptureSet.IsClosed cs -> CaptureBound.IsClosed cb -> Exp.IsClosed e ->
    Exp.IsClosed (.cabs cs cb e)
| consumer : CaptureSet.IsClosed cs -> Ty.IsClosed T -> Exp.IsClosed e ->
    Exp.IsClosed (.consumer cs T e)
| boxed : CaptureSet.IsClosed cs -> ModalCtx.IsClosed Ψ -> Exp.IsClosed e ->
    Exp.IsClosed (.boxed cs Ψ e)
| reader : Var.IsClosed x -> Exp.IsClosed (.reader x)
| alloc : Var.IsClosed x -> Exp.IsClosed (.alloc x)
| drop : Var.IsClosed x -> Exp.IsClosed (.drop x)
| pack : {s : Sig} -> {n : Nat} -> {css : List.Vector (CaptureSet s) n} -> {x : Var .var s} ->
    (∀ cs ∈ css.toList, CaptureSet.IsClosed cs) -> Var.IsClosed x ->
    Exp.IsClosed (.pack css x)
| app : Var.IsClosed x -> Var.IsClosed y -> Exp.IsClosed (.app x y)
| tapp : Var.IsClosed x -> PureTy.IsClosed T -> Exp.IsClosed (.tapp x T)
| capp : Var.IsClosed x -> CaptureSet.IsClosed cs -> Exp.IsClosed (.capp x cs)
| consumer_app : Var.IsClosed x -> Exp.IsClosed e -> Exp.IsClosed (.consumer_app x e)
| unwrap : Var.IsClosed x -> Exp.IsClosed (.unwrap x)
| letin : Exp.IsClosed e1 -> Exp.IsClosed e2 -> Exp.IsClosed (.letin e1 e2)
| unpack : {s : Sig} -> {n : Nat} -> {e1 : Exp s} -> {e2 : Exp ((s.extendCVars n),x)} ->
    Exp.IsClosed e1 -> Exp.IsClosed e2 -> Exp.IsClosed (.unpack n e1 e2)
| unit : Exp.IsClosed .unit
| btrue : Exp.IsClosed .btrue
| bfalse : Exp.IsClosed .bfalse
| nat : Exp.IsClosed (.nat n)
| read : Var.IsClosed x -> Exp.IsClosed (.read x)
| write : Var.IsClosed x -> Var.IsClosed y -> Exp.IsClosed (.write x y)
| cond : Var.IsClosed x -> Exp.IsClosed e2 -> Exp.IsClosed e3 -> Exp.IsClosed (.cond x e2 e3)
| par : CaptureSet.IsClosed C1 -> CaptureSet.IsClosed C2 ->
    Exp.IsClosed e1 -> Exp.IsClosed e2 -> Exp.IsClosed (.par C1 C2 e1 e2)
| arr : (∀ x ∈ xs, Var.IsClosed x) -> Exp.IsClosed (.arr xs)
| idx : Var.IsClosed x -> Var.IsClosed d -> Exp.IsClosed (.idx x n d)
| concat : Var.IsClosed x -> Var.IsClosed y -> Exp.IsClosed (.concat x y)
| split : Var.IsClosed x -> Var.IsClosed y -> Exp.IsClosed (.split x y)
| pair : Var.IsClosed x -> Var.IsClosed y -> Exp.IsClosed (.pair x y)
| fst : Var.IsClosed x -> Exp.IsClosed (.fst x)
| snd : Var.IsClosed x -> Exp.IsClosed (.snd x)

end CoreCapybara
